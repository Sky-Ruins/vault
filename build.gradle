plugins {
  id 'net.minecrell.licenser' version '0.3' apply false
}

allprojects {
  apply plugin: 'idea'
  apply plugin: 'eclipse'

  group = 'com.neolumia'
  description = 'Vault is a Permissions, Chat and Economy API'
  version = '1.6.2'
}

ext {
  url = 'https://neolumia.com/'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'net.minecrell.licenser'

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
  }

  dependencies {
    compileOnly group: 'org.bukkit', name: 'bukkit', version: '1.9-R0.1-SNAPSHOT'
    testCompile group: 'org.bukkit', name: 'bukkit', version: '1.9-R0.1-SNAPSHOT'
  }

  processResources {
    from rootProject.file('LICENSE')
  }

  license {
    header = rootProject.file('HEADER.txt')
    include '**/*.java'
    newLine = true
  }

  task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  artifacts {
    archives sourceJar
    archives javadocJar
  }

  apply from: rootProject.file('gradle/deploy.gradle')
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.4'
}
