buildscript {
  repositories {
    mavenCentral()
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath group: 'kr.motd.gradle', name: 'sphinx-gradle-plugin', version: '1.0.6'
    classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow', version: '2.0.2'
  }
}

apply plugin: 'kr.motd.sphinx'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
  maven { url 'https://repo.bstats.org/content/repositories/releases/' }
}

dependencies {
  compile project(':vault-api')
  compile group: 'org.bstats', name: 'bstats-bukkit', version: '1.1'

  // Libraries
  compileOnly fileTree(dir: 'libs', include: ['*.jar'])
  compileOnly group: 'com.greatmancode', name: 'craftconomy3', version: '3.2.2-SNAPSHOT'
}

license {
  exclude 'net/milkbowl/vault/util/VersionComparator.java'
}

processResources {
  expand(version: version, description: project.description)
}

shadowJar {
  classifier = 'shaded'
  relocate 'org.bstats', 'net.milkbowl.vault'
}

build.dependsOn(shadowJar)
